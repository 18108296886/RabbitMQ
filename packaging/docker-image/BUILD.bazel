load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_image_index")

FIRECRACKER_EXEC_PROPS = {
    # https://www.buildbuddy.io/docs/rbe-microvms
    "workload-isolation-type": "firecracker",
    "EstimatedFreeDiskBytes": "16GB",
    "init-dockerd": "true",
    "recycle-runner": "true",
    # Use the default buildbuddy RBE image
    "container-image": "",
}

filegroup(
    name = "context-files",
    srcs = [
        "10-defaults.conf",
        "20-management_agent.disable_metrics_collector.conf",
        "Dockerfile",
        "docker-entrypoint.sh",
        "//:package-generic-unix",
    ],
)

_ARCHS = [
    "amd64",
    "arm64",
]

[
    genrule(
        name = "docker-build-%s" % arch,
        srcs = [
            ":context-files",
        ],
        outs = [
            "image-%s.tar" % arch,
        ],
        cmd = """set -euo pipefail

CONTEXT="$$(mktemp -d)"

cp $(locations :context-files) "$$CONTEXT"

docker buildx \\
    build \\
    "$$CONTEXT" \\
    --platform linux/{arch} \\
    --tag {tag}:{arch} \\
    --output type=tar,dest=$(location image-{arch}.tar)
""".format(
            arch = arch,
            tag = "bazel/%s/docker-build" % package_name(),
        ),
        exec_properties = FIRECRACKER_EXEC_PROPS,
        tags = [
            "docker",
            "manual",
            "no-sandbox",
            "no-remote-exec", # buildbuddy runners do not have the emulator available
        ],
    )
    for arch in _ARCHS
]

[
    oci_image(
        name = "rabbitmq-%s" % arch,
        architecture = arch,
        os = "linux",
        tags = [
            "docker",
            "manual",
        ],
        tars = [":image-%s.tar" % arch],
    )
    for arch in _ARCHS
]

oci_image_index(
    name = "rabbitmq",
    images = [
        ":rabbitmq-amd64",
        ":rabbitmq-arm64",
    ],
    tags = [
        "docker",
        "manual",
    ],
)

container_structure_test(
    name = "rabbitmq_test",
    configs = ["//packaging/docker-image/test_configs:rabbitmq_ubuntu.yaml"],
    exec_properties = FIRECRACKER_EXEC_PROPS,
    image = ":rabbitmq",
    tags = [
        "docker",
        "manual",
    ],
)
