load("@container_structure_test//:defs.bzl", "container_structure_test")
load(
    "@rules_oci//oci:defs.bzl",
    "oci_image",
    "oci_image_index",
    "oci_push",
    "oci_tarball",
)

filegroup(
    name = "context-files",
    srcs = [
        "10-defaults.conf",
        "20-management_agent.disable_metrics_collector.conf",
        "Dockerfile",
        "docker-entrypoint.sh",
        "//:package-generic-unix",
    ],
)

_ARCHS = [
    "amd64",
    "arm64",
]

_TAGS = [
    "docker",
    "manual",
    "no-sandbox",
    "no-remote-exec",  # buildbuddy runners do not have the emulator available
]

[
    genrule(
        name = "docker-build-%s" % arch,
        srcs = [
            ":context-files",
        ],
        outs = [
            "image-%s.tar" % arch,
        ],
        cmd = """set -euo pipefail

CONTEXT="$$(mktemp -d)"

cp $(locations :context-files) "$$CONTEXT"

docker buildx \\
    build \\
    "$$CONTEXT" \\
    --platform linux/{arch} \\
    --tag {tag}:{arch} \\
    --output type=tar,dest=$(location image-{arch}.tar) $${{EXTRA_BUILDX_OPTS:-}}
""".format(
            arch = arch,
            tag = "bazel/%s/docker-build" % package_name(),
        ),
        tags = _TAGS,
    )
    for arch in _ARCHS
]

[
    oci_image(
        name = "image-%s" % arch,
        architecture = arch,
        os = "linux",
        tags = _TAGS,
        tars = [":image-%s.tar" % arch],
    )
    for arch in _ARCHS
]

[
    oci_tarball(
        name = "rabbitmq-%s" % arch,
        image = ":image-%s" % arch,
        repo_tags = ["bazel/packaging/docker-image:rabbitmq-%s" % arch],
        tags = _TAGS,
    )
    for arch in _ARCHS
]

oci_image_index(
    name = "image",
    images = [
        ":image-%s" % arch
        for arch in _ARCHS
    ],
    tags = _TAGS,
)

oci_tarball(
    name = "rabbitmq",
    format = "oci",
    image = ":image",
    repo_tags = ["bazel/packaging/docker-image:rabbitmq"],
    tags = _TAGS,
)

[
    container_structure_test(
        name = "rabbitmq_test_%s" % arch,
        configs = ["//packaging/docker-image/test_configs:rabbitmq_ubuntu.yaml"],
        image = ":image-%s" % arch,
        tags = _TAGS,
    )
    for arch in _ARCHS
]

oci_push(
    name = "push",
    image = ":image",
    repository = "index.docker.io/pivotalrabbitmq/rabbitmq",
    tags = _TAGS,
)
